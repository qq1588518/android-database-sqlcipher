apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def propertyWithDefault(name, defVal) {
    return rootProject.hasProperty(name) ? rootProject.getProperty(name) : defVal;
}

bintray {
    publications = ['sqlcipherLibrary']

    user = propertyWithDefault('BINTRAY_USER', '')
    key = propertyWithDefault('BINTRAY_APIKEY', '')
    pkg {
        repo = 'sqlcipher'
        name = 'sqlcipher-android'
        desc = ''
        websiteUrl = 'https://github.com/sqlcipher'
        issueTrackerUrl = 'https://github.com/sqlcipher/issues'
        vcsUrl = 'https://github.com/sqlcipher/sqlcipher.git'
        licenses = ['BSD 3-Clause']

        version {
            name = rootProject.VERSION_NAME
            released = new Date()
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name 'BSD 3-Clause License'
            url 'https://opensource.org/licenses/BSD-3-Clause'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id propertyWithDefault('DEVELOPER_ID', '')
            name propertyWithDefault('DEVELOPER_NAME', '')
            email propertyWithDefault('DEVELOPER_EMAIL', '')
        }
    }

    scm {
        url 'https://github.com/sqlcipher'
    }
}

publishing {
    publications {
        wcdbLibrary(MavenPublication) {
            groupId 'net.sqlcipher'
            artifactId 'sqlcipher-android'
            version rootProject.VERSION_NAME

            pom {
                packaging 'jar'

                withXml {
                    def root = asNode()
                    root.appendNode('description', '')
                    root.appendNode('name', 'sqlcipher')
                    root.appendNode('url', 'https://github.com/sqlcipher')
                    root.children().last() + pomConfig

                    def depNode = root.appendNode('dependencies')
                    configurations.compile.allDependencies.each {
                        if (it.group != null) {
                            def dependencyNode = depNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

afterEvaluate {
    publishing.publications.wcdbLibrary {
        artifact(bundleRelease)
        artifact(bundleReleaseJavadoc)
        artifact(bundleReleaseSources)
        artifact(bundleReleaseNativeSymbols)

        artifact(bundleDebug)
        artifact(bundleDebugNativeSymbols)
    }

    tasks.withType(PublishToMavenRepository) { task ->
        task.dependsOn build
    }
    tasks.withType(PublishToMavenLocal) { task ->
        task.dependsOn build
    }

    bintrayUpload {
        for (def pub : bintray.publications) {
            dependsOn "publish${pub.capitalize()}PublicationToMavenLocal"
        }
    }
}
